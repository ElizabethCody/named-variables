plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    jcenter()
}

dependencies {
    testImplementation 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

def pomConfig = {
    licenses {
        license {
            name 'The MIT License'
            url 'https://spdx.org/licenses/MIT.html'
            distribution 'repo'
        }
    }

    developers {
        developer {
            id 'maxwellcody'
            name 'Maxwell Cody'
            email 'maxwell@cody.sh'
        }
    }

    scm {
        url projectScmUrl
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            artifactId projectName
            groupId projectGroup
            version projectVersion

            pom.withXml {
                def root = asNode()
                root.appendNode('name', projectName)
                root.appendNode('description', '')
                root.appendNode('url', projectUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    pkg {
        repo = 'java-libraries'
        name = projectName
        userOrg = 'maxwellcody'
        licenses = ['MIT']
        vcsUrl = projectScmUrl
        version {
            name = projectVersion
            desc = projectVersion
            released = new Date()
        }
    }
}
